/*******************************************************************************************
  SIMPL+ Module Information
  (Fill in comments below)
*******************************************************************************************/
/*
Dealer Name:
System Name: Common_Functions
System Number: 0.1
Programmer: Neil Carthy (arduino@scpgwiki.com)
Comments:                                             
    
	***** License *****

	This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/
/*******************************************************************************************
  Compiler Directives
  (Uncomment and declare compiler directives as needed)
*******************************************************************************************/
#PRINT_TO_TRACE
#ENABLE_DYNAMIC
#DEFAULT_VOLATILE
#ENABLE_STACK_CHECKING
#ENABLE_TRACE
                
//Useful characters that we'll need in parsing metadata
#DEFINE_CONSTANT CR 0x0D		 	// Carriage return
#DEFINE_CONSTANT LF 0x0A		 	// Linefeed
#DEFINE_CONSTANT TAB 0x09		 	// Tab
#DEFINE_CONSTANT SPACE 0x20	   		// Space
#DEFINE_CONSTANT DBLQUOTE 0x22	   	// "
#DEFINE_CONSTANT BACKSLASH 0x5C	   	// \
                  
/* Types of playing media, output via Currently_Playing_Type*/
#DEFINE_CONSTANT TYPE_NONE							 0
#DEFINE_CONSTANT TYPE_MOVIE							 1
#DEFINE_CONSTANT TYPE_AUDIO							 2
#DEFINE_CONSTANT TYPE_SERIES					     3
#DEFINE_CONSTANT TYPE_PICTURES						 4
#DEFINE_CONSTANT TYPE_OTHERVIDEO					 5

/* REequest IDs for different JSON-RPC commands */
#DEFINE_CONSTANT REQUESTID_GETACTIVEPLAYERS				1
#DEFINE_CONSTANT REQUESTID_GETPLAYERPROPERTIES			2
#DEFINE_CONSTANT REQUESTID_AUDIOPLAYLIST_GETITEMS		3
#DEFINE_CONSTANT REQUESTID_VIDEOPLAYLIST_GETITEMS		4
#DEFINE_CONSTANT REQUESTID_GETTIME_SECONDS				5
#DEFINE_CONSTANT REQUESTID_TIME_PERCENTAGE				6
#DEFINE_CONSTANT REQUESTID_MOVIELIBRARY_GETMOVIES		7 // Request Ids 7-20 are handled by XBMC-Browse
#DEFINE_CONSTANT REQUESTID_MOVIELIBRARY_GETMOVIEDETAILS	8 // Request Ids 7-20 are handled by XBMC-Browse
#DEFINE_CONSTANT REQUESTID_MOVIELIBRARY_GETTVSHOWS		9 // Request Ids 7-20 are handled by XBMC-Browse
#DEFINE_CONSTANT REQUESTID_MOVIELIBRARY_GETTVSEASONS	10 // Request Ids 7-20 are handled by XBMC-Browse
#DEFINE_CONSTANT REQUESTID_MOVIELIBRARY_GETTVEPISODES	11 // Request Ids 7-20 are handled by XBMC-Browse
#DEFINE_CONSTANT REQUESTID_MOVIELIBRARY_GETEPISODEDETS	12 // Request Ids 7-20 are handled by XBMC-Browse
#DEFINE_CONSTANT REQUESTID_AUDIOLIBRARY_GETARTISTS		13 // Request Ids 7-20 are handled by XBMC-Browse
#DEFINE_CONSTANT REQUESTID_AUDIOLIBRARY_GETALBUMS		14 // Request Ids 7-20 are handled by XBMC-Browse
#DEFINE_CONSTANT REQUESTID_AUDIOLIBRARY_GETSONGS		15 // Request Ids 7-20 are handled by XBMC-Browse
#DEFINE_CONSTANT REQUESTID_PLAYLIST_CLEAR				16 // Request Ids 7-20 are handled by XBMC-Browse
#DEFINE_CONSTANT REQUESTID_PLAYLIST_ADD					17 // Request Ids 7-20 are handled by XBMC-Browse
#DEFINE_CONSTANT REQUESTID_PLAYLIST_PLAY				18 // Request Ids 7-20 are handled by XBMC-Browse
#DEFINE_CONSTANT REQUESTID_PLAYLIST_PLAYALLLIST			19 // Request Ids 7-20 are handled by XBMC-Browse
#DEFINE_CONSTANT REQUESTID_AUDIOLIBRARY_GETARTISTALBUMS 20 // Request Ids 7-20 are handled by XBMC-Browse

#DEFINE_CONSTANT REQUESTID_SKIPNEXT						22
#DEFINE_CONSTANT REQUESTID_SKIPPREV						23
#DEFINE_CONSTANT REQUESTID_SMALLSKIPFORWARD				24
#DEFINE_CONSTANT REQUESTID_SMALLSKIPBACKWARD			25
#DEFINE_CONSTANT REQUESTID_LARGESKIPFORWARD				26
#DEFINE_CONSTANT REQUESTID_LARGESKIPBACKWARD			27
#DEFINE_CONSTANT REQUESTID_VIDEOLIBRARY_SCAN			28
#DEFINE_CONSTANT REQUESTID_AUDIOLIBRARY_SCAN			29

#DEFINE_CONSTANT REQUESTID_PING							100 // PING

/* Types of JSON response */
#DEFINE_CONSTANT JSON_RESULT							1
#DEFINE_CONSTANT JSON_ERROR								2
#DEFINE_CONSTANT JSON_NOTIFY							3

/* Types of XBMC Playlist/Player type */
#DEFINE_CONSTANT XBMC_ACTIVEPLAYER_NONE					255
#DEFINE_CONSTANT XBMC_ACTIVEPLAYER_AUDIO				0
#DEFINE_CONSTANT XBMC_ACTIVEPLAYER_VIDEO				1
#DEFINE_CONSTANT XBMC_ACTIVEPLAYER_SLIDESHOW			2
#DEFINE_CONSTANT XBMC_ACTIVEPLAYER_AUDIO_s				"0"
#DEFINE_CONSTANT XBMC_ACTIVEPLAYER_VIDEO_s				"1"
#DEFINE_CONSTANT XBMC_ACTIVEPLAYER_SLIDESHOW_s			"2"


/*
    Remove leading and trailing whitespace and non-printable characters
*/
STRING_FUNCTION TRIM(STRING IN$)
{
	INTEGER I,J;		// loop variables    
	INTEGER Length;		// length of input string
	INTEGER B;			// byte value

	Length = LEN(IN$);    
    
	// leading whitespace
	FOR (I = 1 TO Length) 
	{
		B = BYTE(IN$,I);

        IF ( B > 32 && B < 127)
			BREAK;
	}
	I = I - 1;
    
	// trailing whitespace
	FOR (J = 1 TO Length) 
	{
		B = BYTE(IN$,Length - J + 1);

        IF ( B > 32 && B < 127)
			BREAK;       
	}
	J = J - 1;   

	IF (I)
	{
		IF (J)
			RETURN (MID(IN$, I+1, Length - I - J ));			
		ELSE
			RETURN (RIGHT(IN$, Length - I));
	}
	ELSE
	{
		IF (J)
			RETURN (LEFT(IN$, Length - J));
			
		ELSE
			RETURN (IN$);
	}

}

/* 
	Remove the double quotes and whitespace at the start
	and end of the given string.
*/
STRING_FUNCTION RemoveDoubleQuotesAndWhitespace(STRING IN$)
{
	INTEGER I,J;		// loop variables    
	INTEGER Length;		// length of input string
	INTEGER B;			// byte value

	Length = LEN(IN$);    
    
	// leading whitespace
	FOR (I = 1 TO Length) 
	{
		B = BYTE(IN$,I);

        IF ( B > 32 && B < 127 && B != 34)
			BREAK;
	}
	I = I - 1;
    
	// trailing whitespace
	FOR (J = 1 TO Length) 
	{
		B = BYTE(IN$,Length - J + 1);

        IF ( B > 32 && B < 127 && B != 34)
			BREAK;       
	}
	J = J - 1;   

	IF (I)
	{
		IF (J)
			RETURN (MID(IN$, I+1, Length - I - J ));			
		ELSE
			RETURN (RIGHT(IN$, Length - I));
	}
	ELSE
	{
		IF (J)
			RETURN (LEFT(IN$, Length - J));
			
		ELSE
			RETURN (IN$);
	}

}

/*
     Convert a number of seconds into a readable time string
*/
STRING_FUNCTION ConvertToTime(INTEGER NumberOfSeconds)
{
	INTEGER hours;
	INTEGER minutes;
	INTEGER seconds;
	INTEGER tmp;
	STRING  OUT$[10];
    
	seconds = NumberOfSeconds MOD 3600;
	hours = (NumberOfSeconds - seconds) / 3600;

	tmp = seconds;    

	seconds = tmp MOD 60;

	minutes = (tmp - seconds) / 60;

	IF ( hours > 0)
        MAKESTRING(OUT$, "%02d:%02d:%02d", hours, minutes, seconds);
	ELSE IF (minutes > 0)
        MAKESTRING(OUT$, "%02d:%02d", minutes, seconds);
	ELSE
		MAKESTRING(OUT$, "00:%02d", seconds);

	RETURN (OUT$);
}
           
/*
	Given a string representation of a floating point number, 
	return a new string that is the number rounded to N places after the decimal point
*/
STRING_FUNCTION Round(STRING Number, INTEGER N)
{
	INTEGER DecimalPointIndex;
	INTEGER NextNumber;				// The value of the digit N+1 places after the decimal point
	INTEGER Length;
	STRING Result$[100];
	LONG_INTEGER BeforeDecimal, AfterDecimal;
	INTEGER I;		
    
	//PRINT("Number: %s, N: %d", Number, N);

	DecimalPointIndex = FIND(".", Number);
	Length = LEN(Number);
	//PRINT("DecimalPointIndex: %d, Length: %d", DecimalPointIndex, Length);

	IF (DecimalPointIndex)
	{
		IF (DecimalPointIndex+N+1 <= Length) 		// Is the string long enough?
		{
            BeforeDecimal = AtoL(LEFT(Number,DecimalPointIndex-1));
			AfterDecimal = AtoL(MID(Number, DecimalPointIndex+1, N));
			
			//PRINT("NextNumber: %s", MID(Number, DecimalPointIndex+N+1 ,1));
        	NextNumber = AtoI(MID(Number, DecimalPointIndex+N+1 ,1));

			IF (NextNumber > 4)
			{
				IF (N)
					AfterDecimal = AfterDecimal + 1;
				ELSE
                    BeforeDecimal = BeforeDecimal+1;

				IF (LEN(LtoA(AfterDecimal)) > N && N)
				{
                    MAKESTRING(Result$, "%ld.",BeforeDecimal+1);
					FOR (I = 1 to N)
					{
						Result$ = Result$ + "0";
					}
				}
				ELSE IF (N = 0)
                    MAKESTRING(Result$, "%ld",BeforeDecimal);
				ELSE
                    MAKESTRING(Result$, "%ld.%ld",BeforeDecimal,AfterDecimal);

				//PRINT("Result: %s", Result$);
				RETURN (Result$);
			}
			ELSE
			{
				IF (N)
					RETURN (LEFT(Number, DecimalPointIndex+N));
				ELSE
					RETURN (LEFT(Number, DecimalPointIndex-1));
			}
		}
		ELSE
		{
			//Avoid case of string like "123."
			IF (DecimalPointIndex = Length)
				RETURN (LEFT(Number, Length-1));
			ELSE
				RETURN (LEFT(Number, DecimalPointIndex+N)); 
		}
	}
	ELSE
		RETURN (Number);
}
  

/*
	Return 1 if the number is even, 0 otherwise
*/
INTEGER_FUNCTION IsEven(INTEGER I)
{
	RETURN (I MOD 2 = 0);
}

/*
	Return 1 if the number is odd, 0 otherwise
*/
INTEGER_FUNCTION IsOdd(INTEGER I)
{
	RETURN (I MOD 2);
}

                            
/*
	A simplistic lookup function that trys to convert Utf8 to extended ascii
	or nearest ascii equivalent.

	Only works for ascii and 2-byte Unicode
*/
INTEGER_FUNCTION ConvertUtf8ToAscii(INTEGER highByte, INTEGER lowByte)
{
	STRING S[20];
	INTEGER Dec;
	INTEGER I;
	INTEGER Bits[7];
	INTEGER PowerOfTwo;

	IF (highByte>224) // 3- or 4-Byte Unicode
	{
		// We can only handle 2-byte unicode at present
		RETURN (95); // underscore '_'			
	}
	ELSE IF (highByte=0)
		RETURN (lowByte);	// ascii (1-Byte unicode)
    
	// Initialize
	SetArray(Bits,0);
         
	// Create binary array
	// See http://en.wikipedia.org/wiki/UTF-8 for details
	MAKESTRING(S, "%c%c", highByte, lowByte);
    Bits[0] = BIT(S, 1, 7);
    Bits[1] = BIT(S, 1, 6);
    Bits[2] = BIT(S, 2, 5);
    Bits[3] = BIT(S, 2, 4);
    Bits[4] = BIT(S, 2, 3);
    Bits[5] = BIT(S, 2, 2);
    Bits[6] = BIT(S, 2, 1);
    Bits[7] = BIT(S, 2, 0);
     
	// Convert to decimal      
	PowerOfTwo = 128;
	Dec = 0;
	FOR (I = 0 TO 7)
	{
    	IF (Bits[I])
			Dec = Dec + PowerOfTwo;

		PowerOfTwo = PowerOfTwo / 2; 			
	}

	//PRINT ("Hex: \\x%s, Dec: %d, chr:%s", ItoHex(Dec), Dec, chr(Dec));

	RETURN (Dec);
}
